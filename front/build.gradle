buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "com.moowork.gradle:gradle-node-plugin:1.2.0"
    }
}

apply plugin: "kotlin2js"
apply plugin: "kotlin-dce-js"
apply plugin: "com.moowork.node"

node {
    download = true
}

repositories {
    maven { url "https://kotlin.bintray.com/kotlin-js-wrappers" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlinVersion"
    compile "org.jetbrains:kotlin-react:$kotlinReactVersion"
    compile "org.jetbrains:kotlin-react-dom:$kotlinReactVersion"
    compile "org.jetbrains:kotlin-react-router-dom:$kotlinReactRouterVersion"
}

kotlin {
    experimental {
        coroutines 'enable'
    }
}

[compileKotlin2Js, compileTestKotlin2Js]*.configure {
    kotlinOptions {
        sourceMap = true
        sourceMapEmbedSources = "always"
        moduleKind = 'commonjs'
        metaInfo = false
    }
}

task prepareWeb(type: Copy, dependsOn: npmInstall) {
    from "$projectDir/src/web"
    into "$buildDir/web"
}

build.dependsOn prepareWeb

task devBuild(dependsOn: [npmInstall, runDceKotlinJs, prepareWeb])

task devServer(type: NpmTask, dependsOn: [npmInstall]) {
    group = 'Application'
    args = ["run", "server"]
}

task bundle(type: NpmTask, dependsOn: [npmInstall]) {
    group = 'Build'
    args = ["run", "bundle"]
    //752kb
}
